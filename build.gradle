/*
 * Copyright (C) 2015 Twitter, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.0'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
        classpath 'io.fabric.gradle.weaving:projectOrBinary:0.0.1'
        classpath 'io.fabric.gradle.release:kit-release:0.0.3'
    }
}

ext {
    commonPlugins = new File(projectDir, 'common-plugins.gradle')
    internalScript = new File(projectDir, 'internal.gradle')

    fabricDir = projectDir.parentFile.parentFile
    qualityScript = new File(fabricDir, 'quality.gradle')
    deployScript = new File(fabricDir, 'deploy.gradle')
    kitsForRelease = [
            ":tweet-composer": false,
            ":tweet-ui": false,
            ":twitter": false,
            ":twitter-core": false,
            ":twitter-unity": false
    ]
}

subprojects { Project subproject ->
    apply from: rootProject.internalScript
    apply plugin: 'io.fabric.gradle.weaving.projectOrBinary'

    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
        mavenLocal()
    }

    // We need to do this so that by the time any project registers a dependency, that project
    // already have this property.
    projectOrBinary {
        useProject = kitsForRelease.findAll({it.getValue()}).isEmpty() ||
                kitsForRelease.get(subproject.path)
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}

ext {
    enableTestCodeCoverage = true
    compileSdkVersion = 22
    targetSdkVersion = 22
    buildToolsVersion = '22.0.1'
    buildNumber = 'dev'

    // Fabric
    fabric = 'io.fabric.sdk.android:fabric:1.3.+@aar'
    fabricTest = 'io.fabric.sdk.android:fabric-test:0.0.+@aar'
    digits = 'com.digits.sdk.android:digits:1.7.+@aar'

    // Sample App
    leakCanary = 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    leakCanaryNoOp = 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
    appCompat = 'com.android.support:appcompat-v7:21.0.2'

    // Tests
    spoon = 'com.squareup.spoon:spoon-client:1.1.10'
    espresso = 'com.android.support.test.espresso:espresso-core:2.0'
    androidTestingSupport = 'com.android.support.test:testing-support-lib:0.1'
    junit = 'junit:junit:4.12'
    junitAddons = 'junit-addons:junit-addons:1.4'
    robolectric = 'org.robolectric:robolectric:3.0'
    mockito = 'org.mockito:mockito-core:1.9.5'
    dexmakerMockito = 'com.google.dexmaker:dexmaker-mockito:1.2'
    mavenAntTasks = 'org.apache.maven:maven-ant-tasks:2.1.3'

    // Other
    picasso = 'com.squareup.picasso:picasso:2.5.2'
    supportV4 = 'com.android.support:support-v4:21.0.0'
    retrofit = 'com.squareup.retrofit:retrofit:1.8.0'
    retrofitMock = 'com.squareup.retrofit:retrofit-mock:1.8.0'
    gson = 'com.google.code.gson:gson:2.2.4'
    okHttp = 'com.squareup.okhttp:okhttp:2.3.0'
    okHttpUrlConnection = 'com.squareup.okhttp:okhttp-urlconnection:2.3.0'
}
